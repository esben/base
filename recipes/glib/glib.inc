# -*- mode:python; -*-
require glib-common.inc

RECIPE_TYPES = "machine sdk"

inherit autotools-autoreconf pkgconfig

SRC_URI += "file://config.site"
SRC_HOST_SITEFILES = "${SRCDIR}/config.site"

AUTORECONF_GENFILES ?= "INSTALL README"
# These files are shipped in the dist tarballs, removed and
# (re)generated by configure, and finally required for proper
# autoreconf operation.  To be able to (re)run autoreconf after
# configure, we fake them using silly stubs.
do_autoreconf_genfiles_touch () {
	for f in $AUTORECONF_GENFILES ; do
		touch $f
        done
}
do_autoreconf[prefuncs] += "do_autoreconf_genfiles_touch"
do_autoreconf_genfiles_rm () {
	for f in $AUTORECONF_GENFILES ; do
		rm $f
        done
}
do_autoreconf[postfuncs] += "do_autoreconf_genfiles_rm"

RECIPE_FLAGS += "ipv6"
EXTRA_OECONF += "${EXTRA_OECONF_IPV6}"
EXTRA_OECONF_IPV6 = "--disable-ipv6"
EXTRA_OECONF_IPV6:USE_ipv6 = ""

RECIPE_FLAGS += "glib_largefile"
EXTRA_OECONF += "${EXTRA_OECONF_LARGEFILE}"
EXTRA_OECONF_LARGEFILE = "--disable-largefile"
EXTRA_OECONF_LARGEFILE:USE_glib_largefile = ""

RECIPE_FLAGS += "glib_iconv"
EXTRA_OECONF += "${EXTRA_OECONF_ICONV}"
EXTRA_OECONF_ICONV = "--with-libiconv=no"
EXTRA_OECONF_ICONV:USE_glib_iconv = "--with-libiconv=gnu"
DEPENDS += "${DEPENDS_ICONV}"
DEPENDS_ICONV:USE_glib_iconv = "libiconv"

RECIPE_FLAGS += "glib_threads"
DEFAULT_USE_glib_threads = "1"
EXTRA_OECONF += "${EXTRA_OECONF_THREADS}"
EXTRA_OECONF_THREADS = "--disable-threads"
EXTRA_OECONF_THREADS:USE_glib_threads = "--with-threads=posix"
DEPENDS += "${DEPENDS_THREADS}"
DEPENDS_THREADS:USE_glib_threads = "libpthread"

RECIPE_FLAGS += "glib_fam"
EXTRA_OECONF += "${EXTRA_OECONF_FAM}"
EXTRA_OECONF_FAM = "--disable-fam"
EXTRA_OECONF_FAM:USE_glib_fam = ""
DEPENDS += "${DEPENDS_FAM}"
DEPENDS_FAM:USE_glib_fam = "libfam"

EXTRA_OECONF += "--with-pcre=system"
DEPENDS += "${DEPENDS_PCRE}"
DEPENDS_PCRE = "libpcre"
